provider "google" {
}

resource "google_compute_network" "vpc_network" {
  name                    = "${var.name}-network"
  auto_create_subnetworks = "true"
}

resource "google_compute_address" "vm_static_ip" {
  name = "${var.name}-static-ip"
}

resource "google_compute_instance" "master-node" {
  name   = "${var.name}-master"
  machine_type   = var.master-instance
  allow_stopping_for_update = true

  boot_disk {
    initialize_params {
      image = var.master-image
      size = var.disk-size
      type = var.disk-type

    }
  }

  network_interface {
    # network = google_compute_network.vpc_network.self_link
    network="default"
    access_config {
      nat_ip = google_compute_address.vm_static_ip.address
    }
  }

  metadata = {
    ssh-keys = "ubuntu:${file(var.ssh-public-key)}"
  }

  tags = concat([
    "http-server", "https-server", "qhub-hpc", "master"
  ], var.tags)
}

resource "google_compute_instance" "worker-nodes" {
  count = var.worker-count

  name   = "${var.name}-worker-${count.index}"
  machine_type   = var.worker-instance
  allow_stopping_for_update = true

  boot_disk {
    initialize_params {
      image = var.worker-image
      size = var.disk-size
      type = var.disk-type

    }
  }

  network_interface {
    network = "default"
    access_config {
    }
  }

  metadata = {
    ssh-keys = "ubuntu:${file(var.ssh-public-key)}"
  }

  tags = concat([
    "qhub-hpc", "worker"
  ], var.tags)
}

resource "local_file" "ansible_inventory" {
  content = <<EOT
# autogenerated by terraform

${google_compute_instance.master-node.name} ansible_host=${google_compute_instance.master-node.network_interface.0.access_config.0.nat_ip} ansible_user=ubuntu ansible_ssh_private_key_file=${var.ssh-private-key}
${join("\n", formatlist("%s ansible_host=%s ansible_user=ubuntu ansible_ssh_private_key_file=${var.ssh-private-key}", google_compute_instance.worker-nodes.*.name, google_compute_instance.worker-nodes.*.network_interface.0.access_config.0.nat_ip))}

[hpc_master]
${google_compute_instance.master-node.name}

[hpc_worker]
${join("\n", formatlist("%s", google_compute_instance.worker-nodes.*.name))}
EOT

  filename = "inventory"
}
